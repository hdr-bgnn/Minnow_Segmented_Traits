from snakemake.utils import min_version
import pandas as pd

min_version("6.1.1")

configfile: "config/config.yaml"

def get_image_names(csv_filename):
    # Read CSV returning dictionary of 'name' -> 'download_url'
    df = pd.read_csv(csv_filename)
    # Create 'name' column by removing the filename extension from 'original_file_name'
    names = df['original_file_name'].apply(lambda x : os.path.splitext(x)[0])
    return names.tolist()

# The first rule defines the goal of the workflow
rule all:
    input:
       "results/presence.absence.matrix.csv",
       "results/sampling.species.in.Burress.csv",
       "results/sampling.minnows.95.blob.3.segments.csv"

# Filter the images to be processed
# This is a checkpoint instead of a rule since selection_criteria_segmented_images cannot be evaluated 
# until after this rule creates minnow.filtered.from.imagequalitymetadata.csv
checkpoint select_minnow_images:
    input:
       image_metadata=config["image_metadata"],
       image_quality_metadata=config["image_quality_metadata"],
       burress=config["burress"]
    output:
       images_minnows=config["images_minnows"],
       burress_images_minnows=config["burress_images_minnows"]
    params:
        script=srcdir("../Scripts/minnowSelectionImageQualityMetadata.R"),
    log:
       "logs/select_minnow_images.log",
    singularity:
        'docker://jbradley/minnow:0.0.1'
    shell:
        "Rscript {params.script} {input.image_metadata} {input.image_quality_metadata} {input.burress:q} {output.images_minnows} {output.burress_images_minnows} > {log} 2>&1"

# import BGNN_Snakemake rules used to create morpology presence.json files for images
module segmentation:
    snakefile:
        github("hdr-bgnn/BGNN_Snakemake", path="Snakefile", tag="f0498d7dd87ca2ec3e5a13f4b2c3b1736323dd8b")
    # setup the segmentation rules to fetch images based on the complete list of images
    config: { "list": config['image_metadata'] }

use rule * from segmentation as seg_*

# defines inputs needed for selection_criteria_segmented_images rule
# requires select_minnow_images to be complete
def selection_criteria_segmented_images_input(wildcards):
    with checkpoints.select_minnow_images.get(**wildcards).output[1].open() as f:
       names = get_image_names(f)
    presence_files = [f"Morphology/Presence/{i}_presence.json" for i in names]
    return {
        "presence_files": presence_files,
        "image_metadata": config["image_metadata"],
        "burress": config["burress"]
    }

rule selection_criteria_segmented_images:
    input:
       unpack(selection_criteria_segmented_images_input)
    output:
       matrix="results/presence.absence.matrix.csv",
       burress_sampling="results/sampling.species.in.Burress.csv",
       minnows_sampling="results/sampling.minnows.95.blob.3.segments.csv",
       pa_sample_dist="Prelim Results/presence.absence.sample.dist.png",
       heatmap_avg="Prelim Results/heatmap.avg.blob.png",
       heatmap_sd="Prelim Results/heatmap.sd.blob.png"
    params:
        script=srcdir("../Scripts/selectionCriteriaSegmentedImages.R"),
    log:
       "logs/selection_criteria_segmented_images.log",
    singularity:
        'docker://jbradley/minnow:0.0.2'
    shell:
        "Rscript {params.script} Morphology/Presence {input.image_metadata} {input.burress:q} {output.matrix} {output.burress_sampling} {output.minnows_sampling} {output.pa_sample_dist:q} {output.heatmap_avg:q} {output.heatmap_sd:q} > {log} 2>&1"
